//  VER: 7.1 71120450 (2013-10-13) - AG 2013-11-04 - upgraded
//  VER 6.4 64241500
//  AG 2013-06-07 -- Added by AG for All future Custom Tools

//  AG 2013-06-07 - ORB001-130523-001 -- Populating Last Sold Dates setting when Invoice is OKed and Saved  >>
global
updating procedure FillLastSoldDates(record IVVc IVp, boolean OKed)
begin
  record MainStockBlock MSb;
  record LastSoldDateVc LSDr, oldLSDr;
  row IVVc IVrw;
  integer i,rwcnt;
  string 10 location;

  // LogText(0,"Last Sold Date Executed"); //TM 

  BlockLoad(MSb);
  location = IVp.Location;
   
  rwcnt = MatRowCnt(IVp);
  for (i=0;i<rwcnt;i=i+1) begin
    MatRowGet(IVp,i,IVrw);
    if (blank(location)) then begin
      if (nonblank(IVrw.Location)) then begin
        location = IVrw.Location;
      end else begin
        location = MSb.MainStock;
      end;
    end;
    if (nonblank(IVrw.ArtCode)) then begin
      ResetLoop(LSDr);
      LSDr.ItemCode = IVrw.ArtCode;
      LSDr.Location = location;
      if (ReadFirstMain(LSDr,2,true)) then begin
        RecordCopy(oldLSDr,LSDr);
        if (OKed) then begin
          LSDr.PrevDate = LSDr.LastDate;
          LSDr.LastDate = IVp.InvDate;
        end else begin
          LSDr.LastDate = LSDr.PrevDate;
        end;
        if (RecordUpdate(oldLSDr,LSDr,false) == 0) then begin end;
      end else begin
        if (OKed) then begin
          RecordNew(LSDr);
          LSDr.ItemCode = IVrw.ArtCode;
          LSDr.Location = location;
          LSDr.LastDate = IVp.InvDate;
          if (RecordInsert(LSDr,false)) then begin end;
        end;
      end;
    end;
  end;

  return;
end;
//  AG 2013-06-07 <<

//Display Item Pics 
global
function Integer OpenArtPic(Integer mwn,record RcVc RepSpec,Boolean selectf)
begin
  Integer res;
  
  if (GuiType!=kGuiiPhone and GuiType!=kGuiAndroid) then begin
    if (IsWebClient) then begin
      res = OpenWindow("HALPicDClass",0,mwn,"","",RepSpec);
    end else begin
      res = OpenWindow("HALPicDClass",0,0,"","",RepSpec);
      if (selectf) then begin
        SelectWindow(mwn);
      end;
    end;
  end;

  OpenArtPic = res;
end;

global
function Integer OpenArchiveList(Integer mwn,record RcVc RepSpec,Boolean selectf)
begin
  Integer res;
  
  if (GuiType!=kGuiiPhone and GuiType!=kGuiAndroid) then begin
    if (IsWebClient) then begin
      res = OpenWindow("ArchiveWClass",0,mwn,"","",RepSpec);
    end else begin
      res = OpenWindow("ArchiveWClass",0,0,"","",RepSpec);
      if (selectf) then begin
        SelectWindow(mwn);
      end;
    end;
  end;

  OpenArchiveList = res;
end;