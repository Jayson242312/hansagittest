external function boolean GetBrokerNameIVVc(Integer,var record IVVc);
external procedure AddTextToAreaNL(string,area);

global
function Boolean IVDClassAfterEditField(Integer wn,string fieldname,Integer fn, Integer rownr,Integer changed)
begin
  Boolean res;
  record BrokerVc Brokerr;
  record IVVc IVr;

  res = inner.IVDClassAfterEditField(wn,fieldname,fn,rownr,changed);

  switch (fieldname) begin
    case "BrNum"://MDS:GREG-9
      GetWindowRecord(wn,IVr); //MDS: GREG-9
      GetBrokerNameIVVc(wn,IVr);
      PutWindowRecord(wn,IVr);
  end;

  IVDClassAfterEditField = res;
  return;
end;

global
updating function LongInt IVVcDispatchDelivery(var record IVVc IVr)
BEGIN
  LongInt res;
  area apost,aresponse;
  row IVVc IVrw;
  record CUVc CUr;
  record INVc INr;
  record LocalDelDriversWPBlock LocalDelDriversWPb;
  integer rwcnt,i;
  string 255 filename,id,path;
  boolean oncef,testf;
  json json_response;

  AddTextToAreaNL("{", apost);
  AddTextToAreaNL("  ""payment_method"": ""other"",", apost);
  AddTextToAreaNL("  ""payment_method_title"": ""Other"",", apost);
  AddTextToAreaNL("  ""set_paid"": false,", apost);

  testf = false;
  CUr.Code = IVr.CustCode;

  if(ReadFirstMain(CUr,1,true)) then begin
    testf = true;
    AddTextToAreaNL("  ""billing"": {", apost);
    AddTextToAreaNL("    ""address_1"": """ & CUr.InvAddr0 & """,", apost);
    AddTextToAreaNL("    ""address_2"": """ & CUr.InvAddr1 & """,", apost);
    AddTextToAreaNL("    ""country"": """ & CUr.DelCountry & """,", apost);
    AddTextToAreaNL("    ""email"": """ & CUr.eMail & """,", apost);
    AddTextToAreaNL("    ""phone"": """ & CUr.Mobile & """", apost);
    AddTextToAreaNL("  },", apost);

    AddTextToAreaNL("  ""shipping"": {", apost);
    AddTextToAreaNL("    ""address_1"": """ & CUr.DelAddr0 & """,", apost);
    AddTextToAreaNL("    ""address_2"": """ & CUr.DelAddr1 & """,", apost);
    AddTextToAreaNL("    ""country"": """ & CUr.DelCountry & """,", apost);
    AddTextToAreaNL("    ""email"": """ & CUr.eMail & """,", apost);
    AddTextToAreaNL("    ""phone"": """ & CUr.Mobile & """", apost);
    AddTextToAreaNL("  },", apost);
  end;

  if(testf) then begin
    AddTextToAreaNL("  ""line_items"": [", apost);

    rwcnt = MatRowCnt(IVr);
    if(rwcnt > 0) then begin
      for(i=0;i<rwcnt;i=i+1) begin
        MatRowGet(IVr,i,IVrw);
        INr.Code = IVrw.ArtCode;
        if(ReadFirstMain(INr,1,true)) then begin
          if(oncef) then begin
            AddTextToAreaNL("    ,", apost);
          end;
          AddTextToAreaNL("    {", apost);
          AddTextToAreaNL("       ""sku"": """ & IVrw.ArtCode & """,", apost);
          AddTextToAreaNL("       ""quantity"": " & IVrw.Quant , apost);
          AddTextToAreaNL("    }", apost);
          oncef = true;
        end;
      end;
    end;

    AddTextToAreaNL("  ]", apost);
    AddTextToAreaNL("}", apost);

    BlockLoad(LocalDelDriversWPb);

    if(LocalDelDriversWPb.Enable == 0) then begin
      testf = false;
    end;

    if(testf) then begin
      path = "/wp-json/wc/v3/orders?consumer_key=" & LocalDelDriversWPb.ConsumerKey & "&consumer_secret=" & LocalDelDriversWPb.ConsumerSecret;

      if(SendWebRequest(LocalDelDriversWPb.Host,443,-1,true,"POST",path,"application/json","",false,apost,aresponse,20)) then begin
        json_response = ParseJSONArea(aresponse);
        
        if(JSONNodeExists(json_response, "id")) then begin
          id = JSONGet(json_response, "id");
          MessageBox(0,"Order has been dispatched for delivery, Please assign a driver. Delivery #" & id);
        end else begin
          MessageBox(0,"Order dispatch error. Delivery should be created manually.");
        end;;
      end else begin
        MessageBox(0,"Order dispatch error. Delivery should be created manually.");
      end;
    end;
  end;

  IVVcDispatchDelivery = res;
  RETURN;
END;

global
updating procedure SendForDeliverysm()
begin
  record IVVc IVr;
  Integer nwn,wn;
  LongInt err;

  wn = CurWindow;
  GetWindowRecord(wn,IVr);
  if (UserCanAction("SendForDelivery",true)) then begin
    if ((WindowState(wn)==Rs_normal) and (IVr.OKFlag!=0)) then begin
      IVVcDispatchDelivery(IVr);
    end else begin
      Beep;
    end;
  end else begin
    MessageBox(0,"You do not have permission to dispatch a dilivery.");
  end;
  return;
end;