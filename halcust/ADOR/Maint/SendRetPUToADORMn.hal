external procedure AddTextToAreaNL(string,area);
external updating procedure SendToAdor(area,var record ADORQueueVc,string);
external function string 255 GMSFromatDate(date);

global
updating procedure SendRetPUToADOR(record RetPUVc RetPUr,record ADORQueueVc ADORQueuer)
begin
  area apost,aresponse;
  record RetPUVc RetPU2r;
  record HALAdorBlock ADRb;
  row RetPUVc RetPUrw;
  row ADORQueueVc ADORQueuerw;
  integer rwcnt,i;
  string 255 filename;
  boolean oncef; 
  
//To stay 
BlockLoad(ADRb);
  AddTextToAreaNL("{",apost);
  AddTextToAreaNL("  ""interfaceName"" : ""OrderInstruction_GregsMotors"",",apost);
  AddTextToAreaNL("  ""returnDetailedData"" : true,",apost);
  AddTextToAreaNL("  ""data"": [{",apost);
  AddTextToAreaNL("    ""ClientNo"": """ & ADRb.ADORClientNo & """,",apost);
  AddTextToAreaNL("    ""CustomerNo"": """ & ADRb.ADORClientNo & """,",apost);
  AddTextToAreaNL("    ""OrderNo"": """ & RetPUr.SerNr & """,",apost);
  AddTextToAreaNL("    ""Priority"": 5,",apost);
  AddTextToAreaNL("    ""DueDate"": """ & GMSFromatDate(RetPUr.TransDate) & """,",apost); // needs to be in yyyy-mm-dd format
  AddTextToAreaNL("    ""OrderSource"": ""SERP"",",apost);
  AddTextToAreaNL("    ""OrderTypeNo"": ""RetGdsSup"",",apost);
  AddTextToAreaNL("    ""OrderTypeDesc"": """ & RetPUr.PUNr & """,",apost);
  AddTextToAreaNL("    ""ShipFromDate"": """ & GMSFromatDate(RetPUr.TransDate) & """,",apost);
  AddTextToAreaNL("    ""DestNo"": """ & RetPUr.VECode & """,",apost);
  AddTextToAreaNL("    ""DestName1"": """ & RetPUr.VEName & """,",apost);
  AddTextToAreaNL("    ""DestName2"": """ & RetPUr.Location & """,",apost);
  AddTextToAreaNL("    ""DestAddress1"": """ & RetPUr.DelAddr0 & """,",apost);
  AddTextToAreaNL("    ""DestAddress2"": """ & RetPUr.DelAddr1 & """,",apost);
  AddTextToAreaNL("    ""DestCity"": """ & RetPUr.FreightCode & """,",apost);
  AddTextToAreaNL("    ""DestTownCode"": """ & RetPUr.FreightCompanyRegNr & """,",apost);
  AddTextToAreaNL("    ""DestPostalCode"": null,",apost);
  AddTextToAreaNL("    ""DestCountryCode"": """ & RetPUr.FreightCompany & """,",apost);
  AddTextToAreaNL("    ""DestHub"": null,",apost);
  AddTextToAreaNL("    ""DestContact"": """ & RetPUr.Comment & """,",apost);
  AddTextToAreaNL("    ""DestTelephone"": null,",apost);
  AddTextToAreaNL("    ""DestRef"": null,",apost);
  AddTextToAreaNL("    ""DestRef2"": null,",apost);
  AddTextToAreaNL("    ""Reference1"": null,",apost);
  AddTextToAreaNL("    ""Reference2"": null,",apost);
  AddTextToAreaNL("    ""Reference3"": null,",apost);
  AddTextToAreaNL("    ""Note1"": null,",apost);
  AddTextToAreaNL("    ""Note2"": null,",apost);
  AddTextToAreaNL("    ""CourierName"": null,",apost);
  AddTextToAreaNL("    ""ServiceLevelCode"": null,",apost);
  AddTextToAreaNL("    ""Lines"": [",apost);
  rwcnt = MatRowCnt(RetPUr);
  if (rwcnt > 0) then begin
    for (i = 0; i < rwcnt; i = i + 1) begin
      MatRowGet(RetPUr, i, RetPUrw);
      if(oncef) then begin 
        AddTextToAreaNL("      ,",apost);
      end; 
      AddTextToAreaNL("      {",apost);
      AddTextToAreaNL("        ""LineNo"": " & i + 1 & " ,",apost); // should start at 1
      AddTextToAreaNL("        ""SKUNo"": """ & RetPUrw.ArtCode & """,",apost);
      AddTextToAreaNL("        ""OrderedQty"":" & RetPUrw.Quant,apost);
      AddTextToAreaNL("       }",apost); 
      oncef = true; 
    end;
      
  end;
  AddTextToAreaNL("     ]",apost);
  AddTextToAreaNL("   }]",apost);
  AddTextToAreaNL("}",apost);
//end of to stay 

  if (RetPUr.ADORLocked != 1) then begin
    RecordCopy(RetPU2r,RetPUr);
    RetPUr.ADORLocked = 1;
    RecordUpdate(RetPU2r,RetPUr,true);
  end;
  CreateRecordLink(RetPUr,CurrentCompany,ADORQueuer,CurrentCompany);
  CreateRecordLink(ADORQueuer,CurrentCompany,RetPUr,CurrentCompany);

  SendToAdor(apost,ADORQueuer,"Inbound");

  return;
end;

global
updating procedure AddOneRetPUToADORQueue(record RetPUVc RetPUr)
begin
  record ADORQueueVc ADORQueuer;
  record RetPUVc RetPU2r;
  row ADORQueueVc ADORQueuerw;
  record HALAdorBlock HALAdorb;
  record LocationVc Locationr;
  string 255 key;
  integer i,rwcnt;
  boolean testf;

  testf = true;
  BlockLoad(HALAdorb);
  if (HALAdorb.ADORIntegr != 1) then begin
    testf = false;
  end;

  if (testf) then begin
    if (RetPUr.OKFlag == 1) then begin
      testf = false;
    end;
  end;

  if (testf) then begin
    Locationr.Code = RetPUr.Location;
    if (ReadFirstMain(Locationr,1,true)) then begin
      if (Locationr.ADORControl != 1) then begin
        testf = false;
      end;
    end;  
  end;
  
  if (testf) then begin
    ADORQueuer.RegisterCode = "RetPUVc";
    ADORQueuer.RecordNumber = RetPUr.SerNr;
    key = "RegCodeNumber";
    if (ReadFirstKey(key,ADORQueuer,2,true)) then begin
      ADORQueuer.Operation = 2; // update
      ADORQueuer.Status = 1; // Queued
      ADORQueuerw.Operation = 2;
      ADORQueuerw.LogDate = CurrentDate;
      ADORQueuerw.LogTime = CurrentTime;
      ADORQueuerw.Details = "Updated";
      MatRowInsert(ADORQueuer,MatRowCnt(ADORQueuer),ADORQueuerw);
    end else begin
      ADORQueuer.SerNr = NextSerNr("ADORQueueVc",CurrentDate,0,false,"");
      ADORQueuer.RegisterCode = "RetPUVc";
      ADORQueuer.RecordNumber = RetPUr.SerNr;
      ADORQueuer.CreationDate = CurrentDate;
      ADORQueuer.CreationTime = CurrentTime;
      ADORQueuer.Operation = 1; // create
      ADORQueuer.Status = 1; // Queued
      ADORQueuerw.Operation = 1;
      ADORQueuerw.LogDate = CurrentDate;
      ADORQueuerw.LogTime = CurrentTime;
      ADORQueuerw.Details = "Created";
      MatRowInsert(ADORQueuer,MatRowCnt(ADORQueuer),ADORQueuerw);
    end;
    LogText(0,"Queued: SendRetPUToADOR");
    SendRetPUToADOR(RetPUr,ADORQueuer);
  end;

  return;
end;

global
updating procedure SendRetPUToADORMn(record RcVc RepSpec)
begin
  record RetPUVc RetPUr;
  boolean TrHs;

  TrHs = true;
  if (BlankDate(RepSpec.sEndDate)) then begin
    RetPUr.TransDate = CurrentDate;  
  end else begin
    RetPUr.TransDate = RepSpec.sEndDate;  
  end;
  while (LoopBackKey("TransDate", RetPUr, 1, TrHs)) begin
    if (TrHs) then begin
      if (NonBlank(RepSpec.sStartDate)) then begin
        if (RetPUr.TransDate < RepSpec.sStartDate) then begin
          TrHs = false;
        end;
      end;
    end;
    if (TrHs) then begin
      if (RetPUr.OKFlag != 1) then begin
        AddOneRetPUToADORQueue(RetPUr);
      end;
    end;
  end;
  return;
end;