//  VER: 7.1 71120450 (2013-10-13) - AG 2013-11-04 - upgraded
//  VER 6.4 64241500

window VNVEReceiptsEClass:1
  wtag("language","ENG"), wtag("product","*"), wtag("productcode","*"), wtag("device","computer")
begin
  real h,v;

  WindowBegin("Export Supplier Receipts (GRV)",VNVEReceiptsEClass,CGEcw,RcType);
  SetWRect(20,80,400,300);
  EndWindow;

  return;
end;

function longint GetORQty(String SerNo, String ItemCode)
begin
  record PUVc PUr;
  row PUVc PUrw;
  integer count,i,Qty;

  PUr.SerNr = SerNo;
  if (ReadFirstMain(PUr,1,true)) then begin
    count = MatRowCnt(PUr);
    for(i=0;i<count;i=i+1)begin
      MatRowGet(PUr,i,PUrw);
      if (ItemCode == PUrw.ArtCode) then begin
        Qty = Qty + PUrw.Quant;
      end;
    end;
  end;

  GetORQty = Qty;
  return;
end;

global
procedure VEReceiptsEn(record RcVc RepSpec)
begin
  record PUVc PUr;
  row PUVc PUrw;
  string 255 PONumber,Quant,SiteCode,ItemCode;
  integer lineNr,count,i,IBTFlag;
  val OutStQty;
  record JEBlock JEb;
  Date CutOffDate;
  Boolean testf;

  BlockLoad(JEb);
  CutOffDate = AddMonth(CurrentDate,-JEb.Months); //  AG 2013-06-24 -- ORB001-130624-001 - Only receipts for number of months in setting
  IBTFlag = 0;
  lineNr = 1;
  SiteCode = "WARE";
  ExportString("GRVNumber");
  ExportString("PurchaseOrderNumber");
  ExportString("SiteCode");
  ExportString("ItemCode");
  ExportString("ReceivedQuantity");
  ExportString("ActualReceiptDate");
  ExportString("IBTSourceSite");
  ExportString("SupplierCode");
  ExportString("PurchaseOrderLineNumber");
  newLine;
  ItemCode = "XXXX";
  testf = false;
  while(LoopMain(PUr,1,true)) begin
    count = MatRowCnt(PUr);
    for(i=0;i<count;i=i+1)begin
      MatRowGet(PUr,i,PUrw);
      if(NonBlank(PUrw.PONr))then begin
        PONumber = PUrw.PONr;
      end;
      if (NonBlank(PUr.PONr)) then begin
        PONumber = PUr.PONr;
      end;
      if(PONumber == "-1")then begin
        PONumber = blankVal;
      end;
      if(Blank(PONumber))then begin
        goto Lend;
      end;
      if(PUr.OKFlag <> 1)then begin
        goto Lend;
      end;
      if (PUr.TransDate > CutOffDate) then begin testf = true; end;
      if ((ItemCode <> PUrw.ArtCode) and (testf == true)) then begin
        ExportString(PUr.SerNr);
        ExportString(PONumber);
        ExportString(PUr.Location);
        ExportString(PUrw.ArtCode);
        Quant = GetORQty(PUr.SerNr,PUrw.ArtCode);
        ExportLongint(Quant);
        ExportString(PUr.TransDate);
        ExportString("");//IBTSiteCode
        ExportString(PUr.VECode);
        ExportLongint("");
        newLine;
        ItemCode = PUrw.ArtCode;
      end;
      Lend:;
    end;
  end;

  CloseFile;
  return;
end;

//  AG 2013-06-24 -- ORB001-130624-001 - Creating the split between Nightly Export and user run exports
global
procedure VEReceipts_NightlyEn(record RcVc RepSpec)
begin
  record JEBlock JEb;

  BlockLoad(JEb);
  if (CurrentCompany == 1) then begin
    CreateFile(JEb.Path & "SupplierReceipts.txt");
  end;
  if (CurrentCompany == 3) then begin
    CreateFile(JEb.Path2 & "SupplierReceipts.txt");
  end;
  VEReceiptsEn(RepSpec);

  return;
end;