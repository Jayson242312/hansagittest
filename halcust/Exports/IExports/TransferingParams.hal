// RP Not done

external inner function LongInt DateDiff(Date,Date);

global
procedure TransferingParamsEn(record RcVc RepSpec) begin
  record LocationVc FrLocationr;
  record LocationVc ToLocationr;
  record StockMovVc SMr;
  Date today;
  Date startdate;
  boolean continue;
  boolean testf;
  integer count;
  longint ave, total, diff;

  continue = true;
  testf = true;
  today = CurrentDate;
  startdate = AddMonth(today,-24);

  ExportString("Source Location");
  ExportString("Destination Location");
  ExportString("Lead Time");
  ExportString("Cost per unit");
  NewLine;

  while(LoopMain(FrLocationr,1,true)) begin
    ResetLoop(ToLocationr);
    ToLocationr.Code = "1000";

    while(LoopMain(ToLocationr,1,true)) begin
      ResetLoop(SMr);
      SMr.TransDate = startdate;
      testf = true;
      total = 0;
      count = 0;

      if(FrLocationr.Code == ToLocationr.Code) then begin testf = false; end;

      if(testf) then begin
        while(LoopKey("TransDate", SMr, 1, true)) begin
          if(
            DateInRange(SMr.TransDate, startdate, today) and
            SMr.FrLocation == FrLocationr.Code and 
            SMr.ToLocation == ToLocationr.Code
          )then begin
            if(NonBlank(SMr.SentTransDate) and NonBlank(SMr.TransDate)) then begin
              diff = DateDiff(SMr.TransDate,SMr.SentTransDate);

              if(diff <= 5) then begin
                count = count + 1;
                total = total + diff;
              end;
            end;
          end;
        end;

        if(count > 0) then begin
          ave = total/count;
          ExportString(FrLocationr.Code);
          ExportString(ToLocationr.Code);
          ExportLongInt((ave + 2));  
          ExportString("");
          NewLine;
        end;
      end;
    end;
  end;

  CloseFile;
end;

global
procedure TransferingParamsIEn(record RcVc RepSpec)
begin
  record JEBlock JEb;

  BlockLoad(JEb);
  if (CurrentCompany == 1) then begin
    CreateFile(JEb.Path & "Transfering Params.txt");
  end;
  if (CurrentCompany == 3) then begin
    CreateFile(JEb.Path2 & "Transfering Params.txt");
  end;
  TransferingParamsEn(RepSpec);

  return;
end;