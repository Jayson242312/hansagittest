external inner function string 255 MonthName(Date);

//
global
procedure CustPaymentsAgedRn(record RcVc RepSpec)
begin
  record IPrsVc IPrsr;
  record IPVc IPr;
  row IPVc IPrw;
  record INVc INr;
  record CUVc CUr; String 25 CustName;
  //
  boolean wloop,found;
  Integer arr_size,count,rwcnt,i,v,j;
  date datePeriod,periodEnd;
  string 255 prevCode,custcode;

  array String 225 arr_custcode; 
  Date sd,ed,getPreviousMonth;
  Date sd_Over120,ed_Over120;
  array val arr_receipt;
  array val arr_receipt30;
  array val arr_receipt60;
  array val arr_receipt90;
  array val arr_receipt120;
  array val arr_receiptOver120;

  array val arr_monthSum;
  array val arr_itemSum;
  
  array val receiptCode;
  array String 225 customerCode;
  boolean receiptUsed;


  // start report job
  StartReportJob("Customer Payments Aged");

  // get cust.name
  if(blank(RepSpec.f1)) then begin
    CustName = "";
  end else begin
    IPrsr.CustCode = RepSpec.f1;
    CUr.Code = RepSpec.f1;
    if(ReadFirstMain(CUr,1,true)) then begin
      CustName = CUr.Name;
    end;
  end;
  
  // no period
  if(blank(RepSpec.f2)) then begin
    RepSpec.f2 = CurrentDate; // default period
  end;

  // set month over 120 days (receipts)
  datePeriod = RepSpec.f2;
  sd_Over120 = AddMonth(datePeriod,-5);
  ed_Over120 = sd_Over120;
  ed_Over120.day = DaysInMonth(GetYear(sd_Over120),GetMonth(sd_Over120));



  //
  receiptUsed = false;
  
  // loop receipt-history
  while (LoopMain(IPrsr,1,true)) begin

    // if cust.code no match
    if((nonblank(RepSpec.f1)) AND (RepSpec.f1 != IPrsr.CustCode)) then begin
      goto BreakL;
    end;
    
    // =========================================== PAYMENTS ===========================================

    // get type receipt
    if (IPrsr.TransType == 1) then begin

      // set code on initial loop
      if(count == 0) then begin
        prevCode = IPrsr.CustCode;
      end;
      count = count + 1;

      //
      for(i=0; i<receiptCode.length; i=i+1) begin
        if(receiptCode[i] == IPrsr.TransNr AND customerCode[i] == IPrsr.CustCode) then begin
          receiptUsed = true;
        end;
      end;

      if(receiptUsed==false) then begin
        
        // find receipt record
        IPr.SerNr = IPrsr.TransNr;
        if(ReadFirstMain(IPr,1,true)) then begin
          //
          receiptCode[receiptCode.length] = IPrsr.TransNr;
          customerCode[customerCode.length] = IPrsr.CustCode;
          
          // loop receipt row
          rwcnt = MatRowCnt(IPr);
          for(v=0; v<rwcnt; v=v+1) begin
            MatRowGet(IPr,v,IPrw);

            // sort receipt by date
            j = 0;
            While(j >= -4) begin
              //
              datePeriod = RepSpec.f2;
              sd = AddMonth(datePeriod,j);
              sd.day = 1;
              ed = sd;
              ed.day = DaysInMonth(GetYear(sd),GetMonth(sd));

              // is transaction within spec.date.range
              if(DateInRange(IPrw.PayDate,sd,ed)) then begin

                // increase arr.subscript
                if(IPrsr.CustCode != prevCode) then begin
                  arr_size = arr_size + 1;
                  prevCode = IPrsr.CustCode;
                end;
                
                // -> CURRENT
                if(j == 0) then begin
                  arr_custcode[arr_size] = IPrsr.CustCode;
                  arr_receipt[arr_size] = arr_receipt[arr_size] + IPrw.RecVal;
                end;

                // -> 30 DAYS
                if(j == -1) then begin
                  arr_custcode[arr_size] = IPrsr.CustCode;
                  arr_receipt30[arr_size] = arr_receipt30[arr_size] + IPrw.RecVal;
                end;

                // -> 60 DAYS
                if(j == -2) then begin
                  arr_custcode[arr_size] = IPrsr.CustCode;
                  arr_receipt60[arr_size] = arr_receipt60[arr_size] + IPrw.RecVal;
                end;

                // -> 90 DAYS
                if(j == -3) then begin
                  arr_custcode[arr_size] = IPrsr.CustCode;
                  arr_receipt90[arr_size] = arr_receipt90[arr_size] + IPrw.RecVal;
                end;

                // -> 120 DAYS
                if(j == -4)  then begin
                  arr_custcode[arr_size] = IPrsr.CustCode;
                  arr_receipt120[arr_size] = arr_receipt120[arr_size] + IPrw.RecVal;
                end;
              end;
              j = j-1;
            end;

            // is transaction after 120 spec.date.range
            if(IPrw.PayDate <= ed_Over120) then begin
            
              // increase arr.subscript
              if(IPrsr.CustCode != prevCode) then begin
                arr_size = arr_size + 1;
                prevCode = IPrsr.CustCode;
              end;

              arr_custcode[arr_size] = IPrsr.CustCode;
              arr_receiptOver120[arr_size] = arr_receiptOver120[arr_size] + IPrw.RecVal;
            end;
          end;
        end;
      end;
      //
      if(receiptUsed==true) then begin
        receiptUsed = false;
      end;
    end;

  LSkip:;
  end;
  BreakL:;


  // get last month
  getPreviousMonth = AddMonth(RepSpec.f2,-1);
  DaysInMonth(GetYear(CurrentDate),GetMonth(CurrentDate));

  periodEnd = RepSpec.f2;
  periodEnd.day = DaysInMonth(GetYear(RepSpec.f2),GetMonth(RepSpec.f2));

  StartFormat(15);
    OutString(1,0,CustName & " Payments Aged, To: " & periodEnd,true);
  EndHeader;
  EndFormat;

  StartFormat(15); // -> show heading
    OutString(0,0,"",false);
    OutString(5,0,"Code",false);
    OutString(50,0,"Customer",false);
    OutString(210,0,MonthName(RepSpec.f2),true);
    OutString(250,0,"30 days",true);
    OutString(290,0,"60 days",true);
    OutString(330,0,"90 days",true);
    OutString(380,0,"120 days",true);
    OutString(430,0,"Over 120 days",true);
    OutString(1,0,"Total",true);
  EndFormat;

  Gray_Divider(0,1);

  for(i=0; i<=arr_custcode.length; i=i+1) begin
  StartFormat(15); // -> show data
    OutString(0,0,"",false);
    OutString(5,"DblCUVc",arr_custcode[i],false);
    // -> no cust.code
    if(blank(arr_custcode[i])) then begin
      OutString(50,0,"",false);
    end else begin
      // -> get cust.name
      CUr.Code = arr_custcode[i];
      if(ReadFirstMain(CUr,1,true)) then begin
        OutString(50,0,CUr.Name,false);
      end;
    end;
    OutString(210,0,arr_receipt[i],true);
    OutString(250,0,arr_receipt30[i],true);
    OutString(290,0,arr_receipt60[i],true);
    OutString(330,0,arr_receipt90[i],true);
    OutString(380,0,arr_receipt120[i],true);
    OutString(430,0,arr_receiptOver120[i],true);
    // -> item total
    arr_itemSum[i] = arr_receipt[i] + arr_receipt30[i] + arr_receipt60[i] + arr_receipt90[i] + arr_receipt120[i] + arr_receiptOver120[i];
    OutString(1,0,arr_itemSum[i],true);

    // -> period total
    arr_monthSum[0] = arr_monthSum[0] + arr_receipt[i];
    arr_monthSum[1] = arr_monthSum[1]  + arr_receipt30[i];
    arr_monthSum[2] = arr_monthSum[2]  + arr_receipt60[i];
    arr_monthSum[3] = arr_monthSum[3]  + arr_receipt90[i];
    arr_monthSum[4] = arr_monthSum[4]  + arr_receipt120[i];
    arr_monthSum[5] = arr_monthSum[5]  + arr_receiptOver120[i];
  EndFormat;
  end;

  Gray_Divider(0,1);

  StartFormat(15); // show sum by period
    OutString(0,0,"",false);
    OutString(5,0,"Total Payments:",false); 
    OutString(50,0,"",false);
    OutString(210,0,arr_monthSum[0],true);
    OutString(250,0,arr_monthSum[1],true);
    OutString(290,0,arr_monthSum[2],true);
    OutString(330,0,arr_monthSum[3],true);
    OutString(380,0,arr_monthSum[4],true);
    OutString(430,0,arr_monthSum[5],true);
    OutString(1,0,(arr_monthSum[0] + arr_monthSum[1] + arr_monthSum[2] + arr_monthSum[3] + arr_monthSum[4] + arr_monthSum[5]),true);
  EndFormat;

  Gray_Divider(0,1);
  
  EndJob;

end;