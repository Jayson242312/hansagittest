external inner procedure HT2Per(Date, Date , var string);

global
procedure OustandingDownPaymentsRn(record RcVc RepSpec)
begin
    record ORVc ORr;
    string 255 tstr;
    boolean continue, testf;
    val total;

    StartReportJob("Outstanding Down Payments");

    HT2Per(RepSpec.sStartDate,RepSpec.sEndDate,tstr);
    Header(1,tstr,1);
    EndHeader; 

    SetRepCol(2,0); 
    SetRepCol(3,60); 
    SetRepCol(4,480); 

    StartFormat(15);
    OutString(2,0,"SerNr",false);
    OutString(3,0,"Name",false);
    OutString(4,0,"Total",true);
    EndFormat;

    Gray_Divider(0,1);
    
    ORr.OrdDate = RepSpec.sStartDate;
    continue = true;
    testf = false;

    while(LoopKey("OrdDate", ORr, 1, continue)) begin
      if(ORr.OrdDate > RepSpec.sEndDate)then begin
        continue = false;
      end;

      if(Blank(RepSpec.f1) and Blank(RepSpec.f2) and (RepSpec.flags[1] == 0) and (RepSpec.flags[2] == 0)) then begin
        testf = true;
      end;

      if(NonBlank(ORr.DownPaySent) and ORr.OrdDate <= RepSpec.sEndDate) then begin
        if(RepSpec.flags[1] == 1)then begin
          if(ORr.ShipFlag == 0)then begin
            testf = true;
          end;
        end;

        if(RepSpec.flags[2] == 1)then begin
          if((ORr.InvFlag==0) or (ORr.InvFlag==1))then begin
            testf = true;
          end;
        end;

        if(testf)then begin
          if(NonBlank(RepSpec.f1)) then begin
            if(ORr.CustCode != RepSpec.f1)then begin
              testf = false;
            end;
          end;

          if(NonBlank(RepSpec.f2)) then begin
            if(ORr.Location != RepSpec.f2)then begin
              testf = false;
            end;
          end;

          if(testf) then begin
            StartFormat(15);
            OutString(2,"DblORVc",ORr.SerNr,false);
            OutString(3,0,ORr.Addr0,false);
            OutVal(4,0,ORr.Sum4,M4Val,true);
            EndFormat;
            total = total + ORr.Sum4;
          end;
        end;
      end;

      testf = false;
    end;

    if(total > 0) then begin
      Gray_Divider(0,1);
      StartFormat(15);
      OutString(400,0,"Total",false);
      OutVal(4,0,total,M4Val,true);
      EndFormat;
      Gray_Divider(0,1);
    end;

    EndJob;
  return; 
end;